@ECHO OFF
SETLOCAL enabledelayedexpansion


@REM :: insert your files here
SET "MDD_FILE=..\tests\working\current\R2401582.mdd"



@REM :: the path where outout files are saved
@REM :: "." means the same directory as this script
@REM :: empty path ("") means the same directory as your MDD
@REM :: temporary files are still created at the location of your MDD (it all is configured within this BAT file - adjust if you need)
@REM :: but temp files are deleted at the end of script (if you have CONFIG_CLEAN_TEMP_MIDDLE_FILES==1==1)

REM SET "OUT_PATH="
SET "OUT_PATH=."




@REM :: adjust config options per your needs
@REM :: when using "if" in BAT files, "1==1" is true and "1==0" is false

@REM :: list some examples of categories that you stack on
SET "CONFIG_PREFER_CATEGORIES=Disney,Mailchimp,Nike"





@REM :: do you need an html file with fields from MDD? It is quite useful
@REM :: set to "1==1" (which means true in bat files) to have this file generated
SET "CONFIG_PRODUCE_HTML_MDD=1==1"

SET "CONFIG_PRODUCE_EXCEL_MDD=1==1"


@REM :: set to "1==1" if you don't need unnecessary files generated by the script - steps taken when produucing final files
@REM :: or, set to "1==0" to have these files stored for debugging
SET "CONFIG_CLEAN_TEMP_MIDDLE_FILES=1==1"


@REM :: config options to specify generated code style

@REM :: unsuppress one of those 2:
@REM :: - to use "operator" ("=*" syntax) - should be the default choice, just because it's faster, and there are no downsides
SET "CONFIG_CODESTYLE_CATCHECK_OP=categorycheck-operator"
@REM :: - or ".ContainsAny()" syntax, which is slower
REM SET "CONFIG_CODESTYLE_CATCHECK_OP=categorycheck-containsany"

@REM :: unsuppress one of those 3:
@REM :: - to check against exact categories listed ( "=* {Disney,Hulu,StarWars}")
@REM ::   this is the fastest, but it takes more effort to support.
@REM ::   If it's a tracker - you'll have to review the list every wave. Obviously, you'll forget to update some of the categories at some question - and some data will be missing in stacked data. This always happens.
REM SET "CONFIG_CODESTYLE_CATCHECK_LIST=categorycheck-explicitcatlist"
@REM :: - or just call "".DefinedCategories()", which is the easiest approach, all is handled automatically
@REM ::   but is the slowest as for processing time
@REM ::   if you are working on a casual study that does not have big number of records - you can choose this option
REM SET "CONFIG_CODESTYLE_CATCHECK_LIST=categorycheck-definedcategories"
@REM :: - or, the most sophisticated option, create a local var with a list of categories in OnJobStart section, and store it in dmgrglobal var
@REM ::   this should be the best, as we don't call ".DefinedCategories()" when processing every record and not wasting computer power
@REM ::   and we don't have to maintain the list of categories, all is updated automatically
@REM ::   in reality, this option is not as fast as expected; explicitcatlist is way faster (if you care about processing time)
SET "CONFIG_CODESTYLE_CATCHECK_LIST=categorycheck-globaldmgrvar"






@REM :: go

SET "CONFIG_CODESTYLE_OPTIONS=@"
IF "%CONFIG_CODESTYLE_CATCHECK_OP%"=="" (
    REM ECHO
) ELSE (
    SET "CONFIG_CODESTYLE_OPTIONS=!CONFIG_CODESTYLE_OPTIONS!,%CONFIG_CODESTYLE_CATCHECK_OP%"
)
IF "%CONFIG_CODESTYLE_CATCHECK_LIST%"=="" (
    REM ECHO
) ELSE (
    SET "CONFIG_CODESTYLE_OPTIONS=!CONFIG_CODESTYLE_OPTIONS!,%CONFIG_CODESTYLE_CATCHECK_LIST%"
)
SET "CONFIG_CODESTYLE_OPTIONS=!CONFIG_CODESTYLE_OPTIONS:@,=!"
if "%CONFIG_CODESTYLE_OPTIONS%"=="" (
    REM ECHO
) ELSE (
    SET CONFIG_CODESTYLE_OPTIONS=--config-code-style "!CONFIG_CODESTYLE_OPTIONS!"
)

IF "%CONFIG_PREFER_CATEGORIES%"=="" (
    REM ECHO
) ELSE (
    SET "CONFIG_PREFER_CATEGORIES=--config-priority-categories !CONFIG_PREFER_CATEGORIES!"
)

REM :: file names with file schemes in json
FOR %%F in ("%MDD_FILE%") DO SET "MDD_FILE_LAST_NAME=%%~nxF"
IF "%OUT_PATH%"=="" (
    SET "OUT_PATH_AND_NAME=%MDD_FILE%"
) ELSE (
    SET "OUT_PATH_AND_NAME=%OUT_PATH%\%MDD_FILE_LAST_NAME%"
)
SET "MDD_FILE_SCHEME=%MDD_FILE%.json"

SET "MDD_FILE_VARIABLES=%MDD_FILE%-stack-var-list-suggested.json"

SET "MDD_FILE_PATCH_STEP401=%MDD_FILE%-patch-401.json"
SET "MDD_FILE_PATCH_STEP402=%MDD_FILE%-patch-402.json"

SET "MDD_FILE_TEMP_STEP401=%MDD_FILE%_401_temp.dms"
SET "MDD_FILE_TEMP_STEP402=%MDD_FILE%_402_temp.dms"

SET "MDD_FILE_RESULT_STEP401=%OUT_PATH_AND_NAME%_401_STKPrep.dms"
SET "MDD_FILE_RESULT_STEP402=%OUT_PATH_AND_NAME%_402_STKCreate.dms"

ECHO -
ECHO 1. read MDD
ECHO read from: %MDD_FILE%
ECHO write to: .json
python dist/mdmautostktoolsap_bundle.py --program read_mdd --mdd "%MDD_FILE%" --config-features label,attributes,properties,scripting --config-section fields --config-contexts Analysis,Question
if %ERRORLEVEL% NEQ 0 ( echo ERROR: Failure && pause && goto CLEANUP && exit /b %errorlevel% )

IF %CONFIG_PRODUCE_HTML_MDD% (
    ECHO -
    ECHO 1.1. generate html
    python dist/mdmautostktoolsap_bundle.py --program report --inpfile "%MDD_FILE_SCHEME%"
    if %ERRORLEVEL% NEQ 0 ( echo ERROR: Failure && pause && goto CLEANUP && exit /b %errorlevel% )
)
IF %CONFIG_PRODUCE_EXCEL_MDD% (
    ECHO -
    ECHO 1.1. generate excel
    python dist/mdmautostktoolsap_bundle.py --program report_excel --inpfile "%MDD_FILE_SCHEME%"
    if %ERRORLEVEL% NEQ 0 ( echo ERROR: Failure && pause && goto CLEANUP && exit /b %errorlevel% )
)

ECHO -
ECHO 2. decide on which questions and loops should be stacked
python dist/mdmautostktoolsap_bundle.py --program mdd-autostk-identify-variables --inp-mdd-scheme "%MDD_FILE_SCHEME%" %CONFIG_PREFER_CATEGORIES% --output-filename "%MDD_FILE_VARIABLES%"
if %ERRORLEVEL% NEQ 0 ( echo ERROR: Failure && pause && goto CLEANUP && exit /b %errorlevel% )

ECHO -
ECHO 3. generate patch file
python dist/mdmautostktoolsap_bundle.py --program mdd-autostk-prepare-patch --inp-mdd-scheme "%MDD_FILE_SCHEME%" --var-list "%MDD_FILE_VARIABLES%" %CONFIG_CODESTYLE_OPTIONS% --output-patch-401 "%MDD_FILE_PATCH_STEP401%" --output-patch-402 "%MDD_FILE_PATCH_STEP402%"
if %ERRORLEVEL% NEQ 0 ( echo ERROR: Failure && pause && goto CLEANUP && exit /b %errorlevel% )

ECHO -
ECHO 4. temporary files with base templates
python dist/mdmautostktoolsap_bundle.py --program mdd-autostk-write-template --action template-401 --output-filename "%MDD_FILE_TEMP_STEP401%"
if %ERRORLEVEL% NEQ 0 ( echo ERROR: Failure && pause && goto CLEANUP && exit /b %errorlevel% )
python dist/mdmautostktoolsap_bundle.py --program mdd-autostk-write-template --action template-402 --output-filename "%MDD_FILE_TEMP_STEP402%"
if %ERRORLEVEL% NEQ 0 ( echo ERROR: Failure && pause && goto CLEANUP && exit /b %errorlevel% )

ECHO -
ECHO 5. patch templates and save as final files

ECHO -
ECHO 5.401 - 401_PreStack.dms script

python dist/mdmautostktoolsap_bundle.py --program mdd-patch --inp-filename "%MDD_FILE_TEMP_STEP401%" --inp-type dms --patch "%MDD_FILE_PATCH_STEP401%" --output-filename "%MDD_FILE_RESULT_STEP401%"
if %ERRORLEVEL% NEQ 0 ( echo ERROR: Failure && pause && goto CLEANUP && exit /b %errorlevel% )

ECHO -
ECHO 5.402 - 402_Stack.dms script

python dist/mdmautostktoolsap_bundle.py --program mdd-patch --inp-filename "%MDD_FILE_TEMP_STEP402%" --inp-type dms --patch "%MDD_FILE_PATCH_STEP402%" --output-filename "%MDD_FILE_RESULT_STEP402%"
if %ERRORLEVEL% NEQ 0 ( echo ERROR: Failure && pause && goto CLEANUP && exit /b %errorlevel% )




ECHO -
ECHO 7 del .json temporary files

IF %CONFIG_CLEAN_TEMP_MIDDLE_FILES% (
    DEL "%MDD_FILE_SCHEME%"
    DEL "%MDD_FILE_VARIABLES%"
    DEL "%MDD_FILE_PATCH_STEP401%"
    DEL "%MDD_FILE_PATCH_STEP402%"
    DEL "%MDD_FILE_TEMP_STEP401%"
    DEL "%MDD_FILE_TEMP_STEP402%"
)

ECHO -
:CLEANUP
ECHO 999. Clean up
REM REM :: comment: just deleting trach .pyc files after the execution - they are saved when modules are loaded from within bndle file created with pinliner
REM REM :: however, it is necessary to delete these .pyc files before every call of the mdmautostktoolsap_bundle
REM REM :: it means, 6 more times here, in this script; but I don't do it cause I have this added to the linliner code - see my pinliner fork
DEL *.pyc
IF EXIST __pycache__ (
DEL /F /Q __pycache__\*
)
IF EXIST __pycache__ (
RMDIR /Q /S __pycache__
)

ECHO done!
exit /b %errorlevel%

